// Copyright (C) 2022 Toitware ApS. All rights reserved.

import uuid
import .firmware

class Device:
  /**
  The hardware ID of the device.

  On the Artemis server, this is the primary key and simply called "id".
  The hardware ID is generated by the Artemis server.

  This ID is generally *not* shared with the user.
  */
  hardware_id/uuid.Uuid

  /**
  The device ID.

  This ID is the one under which users identify a device.
  It's also called "alias" in the Artemis server.
  */
  id/uuid.Uuid

  /**
  The organization ID of the device.

  The ID in which organization the device is registered in.
  */
  organization_id/uuid.Uuid

  constructor --.hardware_id --.id --.organization_id:

/**
A detailed version of the $Device class.
*/
class DeviceDetailed extends Device:
  /**
  The state the device should try to reach.

  This is the configuration that the broker currently sends to the device.
  May be null if the device was never instructed to change state.
  */
  goal/Map?

  /**
  The goal state as reported by the device.

  This might be different from the actual goal state if the device either
    didn't yet receive the new goal state, or if the device didn't report its
    current state yet.

  May be null if the device has not reported a state yet, or if
    the device already applied all the changes.
  */
  reported_state_goal/Map?

  /**
  The current state of the device.

  May be null if the device has not reported a state yet, or if
    the device's firmware state is equal to the current state.
  */
  reported_state_current/Map?

  /**
  The device's firmware state.
  May be null if the device has not reported a state yet.
  */
  reported_state_firmware/Map?

  /**
  The firmware that is installed but not yet running.

  The device has updated its firmware but has not yet rebooted.
  */
  pending_firmware/string?

  /**
  Constructs a new detailed device from the current goal and the
    reported state.
  */
  constructor --.goal/Map? --state/Map?:
    assert: goal or state

    reported_state_goal = state and state.get "goal-state"
    reported_state_current = state and state.get "current-state"
    reported_state_firmware = state and state.get "firmware-state"
    pending_firmware = state and state.get "pending-firmware"

    initial_state := reported_state_firmware ? null : state
    local_organization_id := ?
    local_hardware_id := ?
    local_id := ?

    if initial_state:
      identity := initial_state["identity"]
      local_organization_id = uuid.parse identity["organization_id"]
      local_hardware_id = uuid.parse identity["hardware_id"]
      local_id = uuid.parse identity["device_id"]
    else:
      old_firmware := Firmware.encoded reported_state_firmware["firmware"]
      device := old_firmware.device_specific "artemis.device"
      local_organization_id = uuid.parse device["organization_id"]
      local_hardware_id = uuid.parse device["hardware_id"]
      local_id = uuid.parse device["device_id"]

    super --hardware_id=local_hardware_id --id=local_id --organization_id=local_organization_id

  pod_id_firmware -> uuid.Uuid?:
    return pod_id_from_state_ reported_state_firmware

  pod_id_current -> uuid.Uuid?:
    return pod_id_from_state_ reported_state_current

  pod_id_goal -> uuid.Uuid?:
    return pod_id_from_state_ reported_state_goal

  pod_id_from_state_ state/Map? -> uuid.Uuid?:
    if not state: return null
    if not state.contains "firmware": return null

    firmware := Firmware.encoded state["firmware"]
    return firmware.pod_id
